cmake_minimum_required(VERSION 3.10)
project(FLStudioRichPresence)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED True)

# Source files
set(SOURCES
    src/main.cpp
    src/config.cpp
    src/monitor.cpp
    src/discord_manager.cpp
)

# Header files (for IDE organization)
set(HEADERS
    src/config.h
    src/monitor.h
    src/discord_manager.h
)

add_executable(FLStudioRichPresence ${SOURCES} ${HEADERS})

# Define some handy Social SDK variables
set(DISCORD_SDK_ROOT "${CMAKE_SOURCE_DIR}/lib/discord_social_sdk")
set(DISCORD_SDK_LIB_DIR "${DISCORD_SDK_ROOT}/lib/release")
set(DISCORD_SDK_BIN_DIR "${DISCORD_SDK_ROOT}/bin/release")
set(DISCORD_SDK_INCLUDE_DIR "${DISCORD_SDK_ROOT}/include")

# Include directories
target_include_directories(FLStudioRichPresence PRIVATE 
    ${DISCORD_SDK_INCLUDE_DIR}
    src/  # Include our source directory for headers
)

# Platform-specific Social SDK library paths
if(WIN32)
    set(DISCORD_LIB_PATH "${DISCORD_SDK_LIB_DIR}/discord_partner_sdk.lib")
    set(DISCORD_SHARED_LIB "${DISCORD_SDK_BIN_DIR}/discord_partner_sdk.dll")
elseif(APPLE)
    set(DISCORD_LIB_PATH "${DISCORD_SDK_LIB_DIR}/libdiscord_partner_sdk.dylib")
    set(DISCORD_SHARED_LIB "${DISCORD_SDK_LIB_DIR}/libdiscord_partner_sdk.dylib")
else() # Linux
    set(DISCORD_LIB_PATH "${DISCORD_SDK_LIB_DIR}/libdiscord_partner_sdk.so")
    set(DISCORD_SHARED_LIB "${DISCORD_SDK_LIB_DIR}/libdiscord_partner_sdk.so")
endif()

# Link the Social SDK library
target_link_libraries(FLStudioRichPresence PRIVATE ${DISCORD_LIB_PATH})

# Platform-specific system libraries
if(WIN32)
    # Link against PSAPI for process enumeration on Windows
    target_link_libraries(FLStudioRichPresence PRIVATE psapi)
    # Define PSAPI_VERSION=1 for correct symbol resolution
    target_compile_definitions(FLStudioRichPresence PRIVATE PSAPI_VERSION=1)
endif()

# Set the runtime search path (RPATH) for Linux and macOS
if(UNIX)
    # Use RPATH when building
    set(CMAKE_BUILD_WITH_INSTALL_RPATH TRUE)
    # Set the RPATH to use the lib directory relative to the executable
    set(CMAKE_INSTALL_RPATH "$ORIGIN")
    if(APPLE)
        set(CMAKE_INSTALL_RPATH "@executable_path")
    endif()
endif()

# Copy Social SDK shared library to output directory, so it's available at runtime.
add_custom_command(TARGET FLStudioRichPresence POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy_if_different
    "${DISCORD_SHARED_LIB}"
    $<TARGET_FILE_DIR:FLStudioRichPresence>
)

# Organize files in IDE
source_group("Header Files" FILES ${HEADERS})
source_group("Source Files" FILES ${SOURCES})